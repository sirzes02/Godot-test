@tool
class_name ItemDropper extends Node2D

const PICKUP = preload("res://items/item_pickup/item_pickup.tscn")

@export var item_data: ItemData:
	set = _set_item_data

var has_dropped: bool = false

@onready var sprite_2d: Sprite2D = $Sprite2D
@onready var persistent_data_handler: PersistentDataHandler = $PersistentDataHandler
@onready var audio_stream_player: AudioStreamPlayer = $AudioStreamPlayer

func _ready() -> void:
	if Engine.is_editor_hint():
		_update_texture()
		return
		
	sprite_2d.visible = false
	persistent_data_handler.data_loaded.connect(_on_data_loaded)

func drop_item() -> void:
	if has_dropped:
		return
	
	has_dropped = true
	var drop = PICKUP.instantiate() as ItemPickup
	drop.item_data = item_data
	add_child(drop)
	drop.picked_up.connect(_on_drop_pickup)
	audio_stream_player.play() 

func _on_drop_pickup() -> void:
	persistent_data_handler.set_value()

func _on_data_loaded() -> void:
	has_dropped = persistent_data_handler.value

func _set_item_data(value: ItemData) -> void:
	item_data = value
	_update_texture()
	
func _update_texture() -> void:
	if Engine.is_editor_hint() and item_data and sprite_2d:
		sprite_2d.texture = item_data.texture
