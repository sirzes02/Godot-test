@tool
@icon("res://gui/dialog_system/icons/star_bubble.svg")
class_name DialogSystemNode extends CanvasLayer

signal finished
signal letter_added(letter: String)

var is_active: bool = false
var text_in_progress: bool = false
var waiting_for_choise: bool = false

var text_speed: float = 0.02
var text_length: int = 0
var plain_text: String

var dialog_items: Array[DialogItem]
var dialog_item_index: int = 0

@onready var dialog_ui: Control = $DialogUI
@onready var rich_text_label: RichTextLabel = $DialogUI/PanelContainer/RichTextLabel
@onready var name_label: Label = $DialogUI/NameLabel
@onready var portrait_sprite: DialogPortrait = $DialogUI/PortraitSprite
@onready var dialog_progress_indicator: PanelContainer = $DialogUI/DialogProgressIndicator
@onready var label: Label = $DialogUI/DialogProgressIndicator/Label
@onready var timer: Timer = $DialogUI/Timer
@onready var audio_stream_player: AudioStreamPlayer = $DialogUI/AudioStreamPlayer
@onready var v_box_container: VBoxContainer = $DialogUI/VBoxContainer

func _ready() -> void:
	if Engine.is_editor_hint():
		if get_viewport() is Window:
			get_parent().remove_child(self)
			return
		return

	timer.timeout.connect(_on_timer_timeout)
	hide_dialog()
	pass

func _unhandled_input(event: InputEvent) -> void:
	if not is_active:
		return
		
	if (
		event.is_action_pressed("interact") or
		event.is_action_pressed("attack") or
		event.is_action_pressed("ui_accept")
	):
		if text_in_progress:
			rich_text_label.visible_characters = text_length
			timer.stop()
			text_in_progress = false
			show_dialog_button_indicator(true)
			
			return
		elif waiting_for_choise:
			return
		
		dialog_item_index += 1
		
		if dialog_item_index < dialog_items.size():
			start_dialog()
		else: 
			hide_dialog()
	pass
	
func show_dialog(_items: Array[DialogItem]) -> void:
	is_active = true
	dialog_ui.visible = true
	dialog_ui.process_mode = Node.PROCESS_MODE_ALWAYS
	dialog_items = _items
	dialog_item_index = 0
	get_tree().paused = true
	await get_tree().process_frame
	
	if dialog_items.size() == 0:
		hide_dialog()
	else:
		start_dialog()
	
	pass
	
func hide_dialog() -> void:
	is_active = false
	v_box_container.visible = false
	dialog_ui.visible = false
	dialog_ui.process_mode = Node.PROCESS_MODE_DISABLED
	get_tree().paused = false
	finished.emit()
	pass
	
func start_dialog() -> void:
	waiting_for_choise = false
	show_dialog_button_indicator(false)
	var _d: DialogItem = dialog_items[dialog_item_index]
	
	if _d is DialogText:
		set_dialog_text(_d as DialogText)
	elif _d is DialogChoice:
		set_dialog_choice(_d as DialogChoice)
	
	start_timer()
	pass
	
func set_dialog_text(_d: DialogText) -> void:
	rich_text_label.text = _d.text
	v_box_container.visible = false
	name_label.text = _d.npc_info.npc_name
	portrait_sprite.texture = _d.npc_info.portrait
	portrait_sprite.audio_pitch_base = _d.npc_info.dialog_audio_pitch
	rich_text_label.visible_characters = 0
	text_length = rich_text_label.get_total_character_count()
	plain_text = rich_text_label.get_parsed_text()
	text_in_progress = true
	pass
	
func set_dialog_choice(_d: DialogChoice) -> void:
	v_box_container.visible = true
	waiting_for_choise = true
	
	for child in v_box_container.get_children():
		child.queue_free()
	
	for i in _d.dialog_branches.size():
		var _new_choice: Button = Button.new()
		v_box_container.add_child(_new_choice)
		_new_choice.text = _d.dialog_branches[i].text
		_new_choice.alignment = HORIZONTAL_ALIGNMENT_LEFT
		_new_choice.pressed.connect(_dialog_choice_selected.bind(_d.dialog_branches[i]))
	
	if Engine.is_editor_hint():
		return
	
	await get_tree().process_frame
	await get_tree().process_frame
	v_box_container.get_child(0).grab_focus()
	
	pass
	
func _dialog_choice_selected(_d: DialogBranch)-> void:
	v_box_container.visible = false
	_d.selected.emit()
	show_dialog(_d.dialog_items)
	pass
	
func _on_timer_timeout() -> void:
	rich_text_label.visible_characters += 1
	
	if rich_text_label.visible_characters <= text_length:
		letter_added.emit(plain_text[rich_text_label.visible_characters - 1])
		start_timer()
	else:
		show_dialog_button_indicator(true)
		text_in_progress = false
	pass
	
func show_dialog_button_indicator(_is_visible: bool) -> void:
	dialog_progress_indicator.visible = _is_visible
	
	if dialog_item_index + 1 < dialog_items.size():
		label.text = "NEXT"
	else:
		label.text = "END"

func start_timer() -> void:
	timer.wait_time = text_speed
	var _char = plain_text[rich_text_label.visible_characters - 1]
	
	if '.!?:;'.contains(_char):
		timer.wait_time *= 4
	else:
		timer.wait_time *= 2
	
	timer.start()
	pass
